---
sort: 6
---

# Exercise3



Exercise 3. Take a look at the [lab tools guide](https://pdos.csail.mit.edu/6.828/2018/labguide.html), especially the section on GDB commands. Even if you're familiar with GDB, this includes some esoteric GDB commands that are useful for OS work.

Set a breakpoint at address 0x7c00, which is where the boot sector will be loaded. Continue execution until that breakpoint. Trace through the code in boot/boot.S, using the source code and the disassembly file obj/boot/boot.asm to keep track of where you are. Also use the x/i command in GDB to disassemble sequences of instructions in the boot loader, and compare the original boot loader source code with both the disassembly in obj/boot/boot.asm and GDB.

Trace into bootmain() in boot/main.c, and then into readsect(). Identify the exact assembly instructions that correspond to each of the statements in readsect(). Trace through the rest of readsect() and back out into bootmain(), and identify the begin and end of the for loop that reads the remaining sectors of the kernel from the disk. Find out what code will run when the loop is finished, set a breakpoint there, and continue to that breakpoint. Then step through the remainder of the boot loader.

`boot loader` 的重要组成部分：`boot.S` 和 `main.c`，其中 `boot.S`是汇编文件， `main.c`是C语言文件。当 BIOS运行完成之后，CPU 的控制权就会转移到`boot.S`文件上。`boot.S`代码如下：

```c
#include <inc/mmu.h>

# Start the CPU: switch to 32-bit protected mode, jump into C.
# The BIOS loads this code from the first sector of the hard disk into
# memory at physical address 0x7c00 and starts executing in real mode
# with %cs=0 %ip=7c00.

.set PROT_MODE_CSEG, 0x8         # kernel code segment selector
.set PROT_MODE_DSEG, 0x10        # kernel data segment selector
.set CR0_PE_ON,      0x1         # protected mode enable flag

.globl start
start:
  .code16                     # Assemble for 16-bit mode
  cli                         # Disable interrupts 
  cld                         # String operations increment
```
其中，`cli` 是boot.S，也是boot loader的第一条指令。这条指令用于把所有的中断都关闭。因为在 BIOS 运行期间有可能打开了中断。此时CPU工作在实模式下。 `cld` 用于指定之后发生的串处理操作的指针移动方向。

```c

  # Set up the important data segment registers (DS, ES, SS).
  xorw    %ax,%ax             # Segment number zero
  movw    %ax,%ds             # -> Data Segment
  movw    %ax,%es             # -> Extra Segment
  movw    %ax,%ss             # -> Stack Segment
```

这几条命令主要是在把三个段寄存器，`ds，es，ss`全部清零，因为经历了BIOS，操作系统不能保证这三个寄存器中存放的是什么数。所以这也是为后面进入保护模式做准备。


```c
  # Enable A20:
  #   For backwards compatibility with the earliest PCs, physical
  #   address line 20 is tied low, so that addresses higher than
  #   1MB wrap around to zero by default.  This code undoes this.
seta20.1:
  inb     $0x64,%al               # Wait for not busy
  testb   $0x2,%al
  jnz     seta20.1

  movb    $0xd1,%al               # 0xd1 -> port 0x64
  outb    %al,$0x64

seta20.2:
  inb     $0x64,%al               # Wait for not busy
  testb   $0x2,%al
  jnz     seta20.2

  movb    $0xdf,%al               # 0xdf -> port 0x60
  outb    %al,$0x60
```

这部分指令就是在准备把CPU的工作模式从实模式转换为保护模式。我们可以看到其中的指令包括`inb，outb`这样的IO端口命令。所以这些指令都是在对外部设备进行操作。查询[memory mapped address](https://bochs.sourceforge.io/techspec/PORTS.LST)可知, 0x64端口属于键盘控制器804x，名称是控制器读取状态寄存器。

```bash
0064	r	KB controller read status (MCA)
		 bit 7 = 1 parity error on transmission from keyboard
		 bit 6 = 1 general timeout
		 bit 5 = 1 mouse output buffer full
		 bit 4 = 0 keyboard inhibit
		 bit 3 = 1 data in input register is command
			 0 data in input register is data
		 bit 2	 system flag status: 0=power up or reset  1=selftest OK
		 bit 1 = 1 input buffer full (input 60/64 has data for 804x)
		 bit 0 = 1 output buffer full (output 60 has data for system)
```


```c
seta20.1:
  inb     $0x64,%al               # Wait for not busy
  testb   $0x2,%al
  jnz     seta20.1
```

其中，上述指令是在不断的检测bit1。bit1的值代表输入缓冲区是否满了，也就是说CPU传送给控制器的数据，控制器是否已经取走了，如果CPU想向控制器传送新的数据的话，必须先保证这一位为0。所以这三条指令会一直等待这一位变为0，才能继续向后运行。

```c
  movb    $0xd1,%al               # 0xd1 -> port 0x64
  outb    %al,$0x64
```
　当0x64端口准备好读入数据后就可以写入数据了，所以这两条指令是把`0xd1`这条数据写入到`0x64`端口中。当向`0x64`端口写入数据时，则代表向键盘控制器`804x`发送指令。这个指令将会被送给`0x60`端口。

```c
D1	dbl   write output port. next byte written  to 0060
        will be written to the 804x output port; the
        original IBM AT and many compatibles use bit 1 of
        the output port to control the A20 gate.
```

D1 指令代表下一次写入`0x60`端口的数据将被写入给`804x`控制器的输出端口。可以理解为下一个写入`0x60`端口的数据是一个控制指令。

```C
seta20.2:
  inb     $0x64,%al               # Wait for not busy
  testb   $0x2,%al
  jnz     seta20.2
```

指令又开始再次等待，等待刚刚写入的指令D1，是否已经被读取了
如果指令被读取了,下面两条指令会向控制器输入新的指令，`0xdf`。通过查询我们看到`0xDF`指令的含义如下:

```bash
DF	sngl  enable address line A20 (HP Vectra only???)
```

这个指令的含义，启用A20线，代表可以进入保护模式了。



```c

  # Switch from real to protected mode, using a bootstrap GDT
  # and segment translation that makes virtual addresses
  # identical to their physical addresses, so that the
  # effective memory map does not change during the switch.
  lgdt    gdtdesc
  movl    %cr0, %eax
  orl     $CR0_PE_ON, %eax
  movl    %eax, %cr0
```

`lgdt gdtdesc`，是把`gdtdesc`这个标识符的值送入全局映射描述符表寄存器 `GDTR` 中。这个 `GDT` 表是处理器工作于保护模式下一个非常重要的表至于这条指令的功能就是把关于GDT表的一些重要信息存放到CPU的GDTR寄存器中，其中包括GDT表的内存起始地址，以及GDT表的长度。这个寄存器由48位组成，其中低16位表示该表长度，高32位表该表在内存中的起始地址。所以`gdtdesc`是一个标识符，标识着一个内存地址。从这个内存地址开始之后的6个字节中存放着GDT表的长度和起始地址。我们可以在这个文件的末尾看到gdtdesc，如下：

```c
# Bootstrap GDT
.p2align 2                                # force 4 byte alignment
gdt:
  SEG_NULL				# null seg
  SEG(STA_X|STA_R, 0x0, 0xffffffff)	# code seg
  SEG(STA_W, 0x0, 0xffffffff)	        # data seg

gdtdesc:
  .word   0x17                            # sizeof(gdt) - 1
  .long   gdt                             # address gdt
```

其中`gdt`是一个标识符，标识从这里开始就是`GDT`表了。可见这个`GDT`表中包括三个表项，分别代表三个段，null seg，code seg，data seg。由于xv6其实并没有使用分段机制，也就是说数据和代码都是写在一起的，所以数据段和代码段的起始地址都是`0x0`，大小都是`0xffffffff=4GB`。

`gdt` 的三个表项调用SEG()子程序来构造GDT表项的。这个子函数定义在mmu.h中，形式如下：
```c
　#define SEG(type,base,lim)                    \
                .word (((lim) >> 12) & 0xffff), ((base) & 0xffff);    \
                .byte (((base) >> 16) & 0xff), (0x90 | (type)),        \
                (0xC0 | (((lim) >> 28) & 0xf)), (((base) >> 24) & 0xff)
```

可见函数需要3个参数，一是type即这个段的访问权限，二是base，这个段的起始地址，三是lim，即这个段的大小界限。

gdt表中的每一个表项的结构如图所示：
```C
struc gdt_entry_struct

	limit_low:   resb 2
	base_low:    resb 2
	base_middle: resb 1
	access:      resb 1
	granularity: resb 1
	base_high:   resb 1

endstruc
```
每个表项一共8字节，其中limit_low就是limit的低16位。base_low就是base的低16位，依次类推，所以我们就可以理解SEG函数为什么要那么写。

然后在gdtdesc处就要存放这个GDT表的信息了，其中0x17是这个表的大小-1 = 0x17 = 23，至于为什么不直接存表的大小24，根据查询是官方规定的。紧接着就是这个表的起始地址gdt。

当加载完`GDT`表的信息到`GDTR`寄存器之后。紧跟着3个操作， 这几步操作明显是在修改`CR0`寄存器的内容。`CR0`寄存器还有`CR1~CR3`寄存器都是80x86的控制寄存器。其中`$CR0_PE`的值定义于"mmu.h"文件中，为`0x00000001`。可见上面的操作是把`CR0`寄存器的bit0置1，`CR0`寄存器的`bit0`是保护模式启动位，把这一位置为1代表保护模式启动。


```C
  # Jump to next instruction, but in 32-bit code segment.
  # Switches processor into 32-bit mode.
  ljmp    $PROT_MODE_CSEG, $protcseg
```

这只是一个简单的跳转指令，这条指令的目的在于把当前的运行模式切换成32位地址模式

```C
  .code32                     # Assemble for 32-bit mode
protcseg:
  # Set up the protected-mode data segment registers
  movw    $PROT_MODE_DSEG, %ax    # Our data segment selector
  movw    %ax, %ds                # -> DS: Data Segment
  movw    %ax, %es                # -> ES: Extra Segment
  movw    %ax, %fs                # -> FS
  movw    %ax, %gs                # -> GS
  movw    %ax, %ss                # -> SS: Stack Segment
```
修改这些寄存器的值。这些寄存器都是段寄存器。具体可查看[寄存器介绍](https://www.eecg.utoronto.ca/~amza/www.mindsec.com/files/x86regs.html)

如果刚刚加载完GDTR寄存器我们必须要重新加载所有的段寄存器的值，而其中CS段寄存器必须通过长跳转指令，来进行加载


```C
  # Set up the stack pointer and call into C.
  movl    $start, %esp
  call bootmain

  # If bootmain returns (it shouldn't), loop.
spin:
  jmp spin

```

接下来的指令就是要设置当前的`esp`寄存器的值，然后准备正式跳转到`main.c`文件中的`bootmain`函数处。我们接下来分析一下这个函数的每一条指令：

```c
 43         // read 1st page off disk
 44         readseg((uint32_t) ELFHDR, SECTSIZE*8, 0);

```
这里面调用了一个函数readseg，这个函数在bootmain之后被定义了：

```c
72  void readseg(uint32_t pa, uint32_t count, uint32_t offset)

```

它的功能从注释上来理解应该是，把距离内核起始地址offset个偏移量存储单元作为起始，将它和它之后的count字节的数据读出送入以pa为起始地址的内存物理地址处。

所以这条指令是把内核的第一个页(4MB = 4096 = SECTSIZE*8 = 512*8)的内容读取的内存地址ELFHDR(0x10000)处。其实完成这些后相当于把操作系统映像文件的elf头部读取出来放入内存中。

读取完这个内核的elf头部信息后，需要对这个elf头部信息进行验证，并且也需要通过它获取一些重要信息。

>  elf文件：elf是一种文件格式，主要被用来把程序存放到磁盘上。是在程序被编译和链接后被创建出来的。一个elf文件包括多个段。对于一个可执行程序，通常包含存放代码的文本段(text section)，存放全局变量的data段，存放字符串常量的rodata段。elf文件的头部就是用来描述这个elf文件如何在存储器中存储。
需要注意的是，你的文件是可链接文件还是可执行文件，会有不同的elf头部格式。

```c
2 if (ELFHDR->e_magic != ELF_MAGIC)
3        goto bad;
```

elf头部信息的magic字段是整个头部信息的开端。并且如果这个文件是格式是ELF格式的话，文件的elf->magic域应该是=ELF_MAGIC的，所以这条语句就是判断这个输入文件是否是合法的elf可执行文件。

```c
4 ph = (struct Proghdr *) ((uint8_t *) ELFHDR + ELFHDR->e_phoff);
```
头部中一定包含Program Header Table。这个表格存放着程序中所有段的信息。通过这个表我们才能找到要执行的代码段，数据段等等

首先elf是表头起址，而phoff字段代表Program Header Table距离表头的偏移量。所以ph可被指定为Program Header Table表头。

```c
5 eph = ph + ELFHDR->e_phnum;
```

由于phnum中存放的是Program Header Table表中表项的个数，即段的个数。所以这步操作是吧eph指向该表末尾。


```c
6 for (; ph < eph; ph++)
    // p_pa is the load address of this segment (as well
    // as the physical address)
7    readseg(ph->p_pa, ph->p_memsz, ph->p_offset);
```

这个for循环就是在加载所有的段到内存中。ph->paddr根据参考文献中的说法指的是这个段在内存中的物理地址。ph->off字段指的是这一段的开头相对于这个elf文件的开头的偏移量。ph->filesz字段指的是这个段在elf文件中的大小。ph->memsz则指的是这个段被实际装入内存后的大小。通常来说memsz一定大于等于filesz，因为段在文件中时许多未定义的变量并没有分配空间给它们。

所以这个循环就是在把操作系统内核的各个段从外存读入内存中。

```c
8 ((void (*)(void)) (ELFHDR->e_entry))();
```

e_entry字段指向的是这个文件的执行入口地址。所以这里相当于开始运行这个文件。也就是内核文件。 自此就把控制权从boot loader转交给了操作系统的内核

## 参考链接
* <https://www.cnblogs.com/ym65536/p/4268951.html>
* <https://en.wikibooks.org/wiki/X86_Assembly/Global_Descriptor_Table>
